# Maintainer: Holo Team

pkgname=steamos-efi
_srctag=jupiter-20260824.1
pkgver=${_srctag#jupiter-}
pkgrel=3
arch=('x86_64')
url='https://gitlab.steamos.cloud/holo/steamos-efi'
pkgdesc='Stage 1 bootloader for SteamOS 3+'
license=('GPL2')
depends=('efibootmgr' 'util-linux' 'coreutils')
makedepends=('git' 'openssh' 'gnu-efi-libs' 'grub' 'fontconfig' 'noto-fonts')
source=("git+ssh://git@gitlab.steamos.cloud/holo/${pkgname}.git#tag=$_srctag")
sha512sums=('5c79e421274658d1eb73c15ba9c0316bc2186e4efb032141d895135282e46d6185fd1108a489db827ffd1b6d4a286a58e1b9f09cb426e86cacf0eb36d832eaeb')

build() {
	cd "${pkgname}"
	autoreconf -vif
	./configure --prefix=/usr --with-release-version="$pkgver"
	make
}

package() {
	cd "${pkgname}"
	make DESTDIR="$pkgdir/" install
	mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
	rmdir "$pkgdir/usr/sbin/"
}
