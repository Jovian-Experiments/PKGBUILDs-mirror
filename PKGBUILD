# Maintainer: Ludovico de Nittis <ludovico.denittis@collabora.com>

pkgname=steamos-atomupd-client
pkgver=0.20260416.1
pkgrel=2
pkgdesc='SteamOS Atomic Update (client) - Atomic update client for SteamOS.'
arch=('any')
url='https://store.steampowered.com/steamos/'
license=('LGPL2.1')
makedepends=('git' 'python-setuptools')
depends=('python>=3.13' 'python<3.14' 'rauc' 'python-semantic-version')
optdepends=(
  'desync'
  'steamos-alias: for steamos-alias compatibility symlinks'
)
provides=("${pkgname}")
conflicts=("${pkgname}")
source=("${pkgname}::git+ssh://git@gitlab.steamos.cloud/holo/${pkgname%-client}.git#tag=v${pkgver}")
sha256sums=('9aa94756ba2f33b45666ff0813ec468832947a3ac413a8093343a4bf5d5186e2')

build() {
  cd "${pkgname}"
  python setup.py build
}

package () {
  cd "${pkgname}"

  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

  install -Dm755 "bin/holo-atomupd-client" "$pkgdir/usr/bin/holo-atomupd-client"
  install -Dm755 "bin/holo-atomupd-mkmanifest" "$pkgdir/usr/bin/holo-atomupd-mkmanifest"

  install -Dm644 "completions/holo-atomupd-client" "$pkgdir/usr/share/bash-completion/completions/holo-atomupd-client"
  install -Dm644 "completions/holo-atomupd-mkmanifest" "$pkgdir/usr/share/bash-completion/completions/holo-atomupd-mkmanifest"
  install -dm755 "$pkgdir/usr/lib/steamos-atomupd"
  install -dm755 "$pkgdir/etc/steamos-atomupd"
  install -Dm644 "examples/client.conf" "$pkgdir/usr/share/holo-atomupd-client/examples/client.conf"
  install -Dm644 "examples/image.manifest.json" "$pkgdir/usr/share/holo-atomupd-client/examples/image.manifest.json"
}
