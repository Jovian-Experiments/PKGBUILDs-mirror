# Maintainer: Holo Team

pkgname=steamos-efi
_srctag=jupiter-20260410.1
pkgver=${_srctag#jupiter-}
pkgrel=1
arch=('x86_64')
url='https://gitlab.steamos.cloud/holo/steamos-efi'
pkgdesc='Stage 1 bootloader for SteamOS 3+'
license=('GPL2')
depends=('efibootmgr' 'util-linux' 'coreutils')
makedepends=('git' 'openssh' 'gnu-efi-libs' 'grub' 'fontconfig' 'noto-fonts')
source=("git+ssh://git@gitlab.steamos.cloud/holo/${pkgname}.git#tag=$_srctag")
sha512sums=('edfcad832e50c449a5510e9ff59e04a311cd77cd910cb95a41de98f79b3a1c11aa7314b9f5c9cce5314b548311cb2025d1df2e7044517ffab40cf6c0fdf6cf60')

build() {
	cd "${pkgname}"
	autoreconf -vif
	./configure --prefix=/usr --with-release-version="$pkgver"
	make
}

package() {
	cd "${pkgname}"
	make DESTDIR="$pkgdir/" install
	mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
	rmdir "$pkgdir/usr/sbin/"
}
