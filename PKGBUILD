# Author : Clayton Craft <clayton@igalia.com>

pkgname='steamos-systemreport'
pkgver=0.18
pkgrel=2
pkgdesc='System report collection tool'
arch=('any')
license=('LGPL2.1')
url='https://gitlab.steamos.cloud/holo/holo'
source=(
  'steamos-systemreport'
  'steamos-systemreport-privileged'
  'com.steampowered.SteamOS.systemreport.policy'
)
sha256sums=('99356d78325a266fd0d885f5bca1deabeab2bb7f3a53760645cb4b7878a51289'
            'ba38397f247c8ae62037291451b9b8e7eeb946a99add7078ebc5f316ccaf203a'
            'b36473e3b77307450e75c59e248dde34d95b69c6c765a425fc30d9e201e23067')

package() {
  depends=(
    'bash'
    'bluez-deprecated-tools'         # hcitool
    'coreutils'                      # dd
    'rauc'                           # rauc status
    'drm_info'                       # drm_info
    'upower'                         # upower
    'wireplumber'                    # wpctl
    'iputils'                        # ping
    'iproute2'                       # ip
    'iwd'                            # iwctl
    'pciutils'                       # lspci
    'usbutils'                       # lsusb
    'systemd'                        # coredumpctl, journalctl
    'procps-ng'                      # ps
    'util-linux'                     # lsblk, hexdump
    'parted'                         # parted
    'smartmontools'                  # smartctl
    'steamos-customizations-jupiter' # steamos-{readonly,dump-info}
    'jupiter-hw-support'             # amd_system_info
    'polkit'
    'zstd'
  )
  install -Dm755 steamos-systemreport -t "$pkgdir"/usr/bin/
  install -Dm755 steamos-systemreport-privileged -t "$pkgdir"/usr/bin/steamos-polkit-helpers
  install -m755 -d "$pkgdir"/usr/share/polkit-1/actions
  install -m644 com.steampowered.SteamOS.systemreport.policy -t "$pkgdir"/usr/share/polkit-1/actions
}
