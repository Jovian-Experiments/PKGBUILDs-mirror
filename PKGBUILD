# Maintainer: Holo Team

pkgbase='steamos-reset'
pkgname='steamos-reset'
_srctag=jupiter-20260924.1
pkgver=${_srctag#jupiter-}
pkgrel=1
arch=('x86_64')
url='https://gitlab.steamos.cloud/holo/steamos-reset'
pkgdesc='Backend and CLI to reset SteamOS to a freshly installed state'
license=('GPL')
depends=('curl' 'bash' 'steamos-efi' 'steamos-atomupd-client' 'jq')
optdepends=(
    'steamos-alias: for steamos-alias compatibility symlinks'
)
makedepends=('git')
source=("${pkgbase}::git+ssh://git@gitlab.steamos.cloud/holo/steamos-reset#tag=${_srctag}")
sha256sums=('2aa562fa1ef19997be5ebf4d099e7e2a5c1854c8c61ff3f90a75631d9a239da7')

build() {
    cd "$pkgbase"
    autoreconf -ivf
    ./configure --prefix=/usr --libexecdir=/usr/lib --sbindir=/usr/bin
    make
}

package() {
    cd "${pkgbase}"
    make DESTDIR="${pkgdir}" install

    find "$pkgdir" -type d -empty -delete
}
